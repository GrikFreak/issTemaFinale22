System sprint2

Request waste_request : waste_request(MATERIAL, TRUCKLOAD)
Reply loadaccept : loadaccept(MATERIAL, TRUCKLOAD)
Reply loadrejected : loadrejected(MATERIAL, TRUCKLOAD)

Request pickup_request : pickup_request(PATH_TO_INDOOR, MATERIAL)
Reply pickup_done : pickup_done(DONE)

Request storage_request : storage_request(PATH_TO_CONTAINER)
Reply storage_done : storage_done(DONE)

Request home_request : home_request(PATH_TO_HOME)
Reply home_done : home_done(DONE)

Dispatch free_Indoor : free_Indoor(FREE)

//dispatch sent by ws to trolley to stop/resume it
Dispatch stop_trolley : stop_trolley(STOP)
Dispatch resume_trolley : resume_trolley(OK)

//dispatch sent by sonar
Dispatch stop : stop(DISTANCE)
Dispatch resume : resume(OK)

Event alarm : alarm(STOP)

//Event containers_weight : containers_weight(GWEIGHT, PWEIGHT)

Event trolley_status : trolley_status(STATUS)
Event trolley_position : trolley_position(POSITION)

Dispatch led_status : led_status(STATUS)

//Per path executor
Request  dopath    : dopath( PATH  )
Reply dopathdone   : dopathdone( ARG )
Reply dopathfail   : dopathfail( ARG )

Context ctxbasicrobot  ip [ host="127.0.0.1" port=8020 ]  
Context ctxraspberrypi ip [ host="192.168.1.131" port=8030 ] 

Context ctxWasteService ip [ host="localhost" port=8025 ]

ExternalQActor pathexec context ctxbasicrobot
ExternalQActor led context ctxraspberrypi

//we use a Mock, cause it's not an Actor
QActor wastetruckmock context ctxWasteService {
	
	[#
		var Material 	= "";
		var TruckLoad 	= 0L;
		var Materials = arrayListOf<String>("glass", "plastic");
	#]
	
	State s0 initial {
		printCurrentMessage
		println("WASTETRUCK | starts.")
		delay 3000
		
		[# 
			val Load = kotlin.random.Random.nextLong(10,100) 
			val randomIndex = kotlin.random.Random.nextInt(Materials.size);
   			val Mat = Materials[randomIndex]
		#]
		
		request wasteservice -m waste_request : waste_request($Mat, $Load)
		println("WASTETRUCK | sent request of $Mat with load $Load.")
	}
	
	Transition t0 whenReply loadaccept -> accepted
				  whenReply loadrejected -> rejected 
				  
	State accepted {
		printCurrentMessage
		onMsg(loadaccept : loadaccept(MATERIAL, TRUCKLOAD)) {
			[#
				Material 	= payloadArg(0);
				TruckLoad 	= payloadArg(1).toLong();
			#]
			println("WASTETRUCK | accepted $Material with load $TruckLoad.")
		}
	}
	
	Transition t1 whenMsg free_Indoor -> leave_Indoor
	
	State rejected {
		printCurrentMessage
		onMsg(loadrejected : loadrejected(MATERIAL, TRUCKLOAD)) {
			[#
				Material 	= payloadArg(0);
				TruckLoad 	= payloadArg(1).toLong();
			#]
			println("WASTETRUCK | rejected $Material load with weight $TruckLoad")
		}
	}
	
	Goto leave_Indoor
	
	State leave_Indoor {
		println("WASTETRUCK | WasteTruck left Indoor area.")
	}

	Goto s0
	
}

QActor wasteservice context ctxWasteService {
	
	[# 
		var MAXPB 	= 500L;
		var MAXGB 	= 500L;
		var CurrentPB = 0L;
		var CurrentGB = 0L;
		var Material = "";
		var PathHome = "";
		var PathIndoor = "";
		var PathContainer = "";
		val XIndoor = 0;
		val YIndoor = 5;
		val XContainerP = 6;
		val YContainerP = 5;
		val XContainerG = 6;
		val YContainerG = 0;
		val XHome = 0;
		val YHome = 0;
		
		var TrolleyStatus = "IDLE"; //IDLE, WORKING, STOPPED
	#]
	
	State init initial {
		printCurrentMessage
		println("WASTESERVICE | Start.")
		run unibo.kotlin.planner22Util.createRoomMapFromTextfile( "map.txt" )	
   		run unibo.kotlin.planner22Util.initAI() 
  		run unibo.kotlin.planner22Util.showCurrentRobotState()
  		forward led -m led_status : led_status(OFF)
	}
	
	Goto s0 
	
	State s0 {
		printCurrentMessage
		println("WASTESERVICE | is waiting for a request..")
	}
	

	Transition t0 whenRequest waste_request -> handle_request	
			
	State handle_request {
		printCurrentMessage
		onMsg(waste_request : waste_request(MATERIAL, TRUCKLOAD)) {
			[#
				Material = payloadArg(0);
				var TruckLoad 	= payloadArg(1).toLong();
			#]
			println("WASTESERVICE | arrived request: $Material with load $TruckLoad")
			if [# Material.equals("plastic") #] {
				if [# TruckLoad + CurrentPB <= MAXPB #]{
					replyTo waste_request with loadaccept : loadaccept($Material, $TruckLoad) 
					[# CurrentPB += TruckLoad #]
					println("WASTESERVICE | current container plastic weight: $CurrentPB")
					
					run unibo.kotlin.planner22Util.setGoal(XIndoor,YIndoor) 
					[# PathIndoor = unibo.kotlin.planner22Util.doPlan().toString()
						.replace(" ","")
						.replace(",","")
						.replace("[","")
						.replace("]","")
					#]  
					
					request transporttrolley -m pickup_request : pickup_request($PathIndoor, $Material)
					println("WASTESERVICE | Sent pickup request to TransportTrolley, indoor path $PathIndoor.")
					
				} else {
					replyTo waste_request with loadrejected : loadrejected($Material, $TruckLoad)
				}
			} else {
				if [# TruckLoad + CurrentGB <= MAXGB #]{
					replyTo waste_request with loadaccept : loadaccept($Material, $TruckLoad) 
					[# CurrentGB += TruckLoad #]
					println("WASTESERVICE | current container glass weight: $CurrentGB")
				
					run unibo.kotlin.planner22Util.setGoal(XIndoor,YIndoor) 
					[# PathIndoor = unibo.kotlin.planner22Util.doPlan().toString()
						.replace(" ","")
						.replace(",","")
						.replace("[","")
						.replace("]","")
					#]  
					
					request transporttrolley -m pickup_request : pickup_request($PathIndoor, $Material)
					[# TrolleyStatus = "WORKING" #]
					//emit trolley_status : trolley_status($TrolleyStatus)
					forward led -m led_status : led_status(BLINKS)
					println("WASTESERVICE | Sent pickup request to TransportTrolley, first path $PathIndoor")
					
				} else {
					replyTo waste_request with loadrejected : loadrejected($Material, $TruckLoad)
				}
			}
			//emit containers_weight : containers_weight(CurrentGB, CurrentPB)
		}
	}
	
	Transition t1 whenReply pickup_done -> handle_storage 
					whenMsg stop -> handle_stop
	
	State handle_storage {
		printCurrentMessage
		forward wastetruckmock -m free_Indoor : free_Indoor(FREE)
		println("WASTESERVICE | Sent message to WasteTruck to leave indoor area.")
		if [# Material.equals("plastic") #]{
			run unibo.kotlin.planner22Util.setGoal(XContainerP,YContainerP) 
			[# PathContainer = unibo.kotlin.planner22Util.doPlan().toString()
				.replace(" ","")
				.replace(",","")
				.replace("[","")
				.replace("]","")
			#]  
		} else {
			run unibo.kotlin.planner22Util.setGoal(XContainerG,YContainerG) 
			[# PathContainer = unibo.kotlin.planner22Util.doPlan().toString()
				.replace(" ","")
				.replace(",","")
				.replace("[","")
				.replace("]","")
			#]  
		}
		println("WASTESERVICE | send request to store to the transport trolley.")
		request transporttrolley -m storage_request : storage_request($PathContainer)
		//forward led -m led_status : led_status(BLINKS)
	}
	
	Transition t2 whenReply storage_done -> storage_Done 
					whenMsg stop -> handle_stop
	
	State storage_Done {
		printCurrentMessage
		println("WASTESERVICE | Storage done. Handle queue...")
	}
	
	Transition t3 whenTime 100 -> move_home
				whenRequest waste_request -> handle_request
				
	State move_home {
		printCurrentMessage
		println("WASTESERVICE | No Queue, send Transport trolley to HOME")
		
		run unibo.kotlin.planner22Util.setGoal(XHome,YHome) 
		
		[#
		PathHome = unibo.kotlin.planner22Util.doPlan().toString() 
			.replace(" ","")
			.replace(",","")
			.replace("[","")
			.replace("]","")
		#]
		request transporttrolley -m home_request: home_request($PathHome)
	}
	
	Transition t4 whenReply home_done -> back_done
					whenMsg stop -> handle_stop
	
	State back_done{
		printCurrentMessage
		println("WASTESERVICE | the trolley is arrived at HOME")
		[# TrolleyStatus = "IDLE" #]
		//emit trolley_status : trolley_status($TrolleyStatus)
		forward led -m led_status : led_status(OFF)
		run  unibo.kotlin.planner22Util.updateMapWithPath( PathHome )
		run  unibo.kotlin.planner22Util.showCurrentRobotState()
	}
	
	Goto s0
	
	State handle_stop {
		println("WASTESERVICE | Trolley stopped by the sonar, wait for resume..")
		//[# TrolleyStatus = "STOPPED" #]
			forward led -m led_status : led_status(ON)
		//emit trolley_status : trolley_status(STOPPED)
		forward transporttrolley -m stop_trolley : stop_trolley(STOP)
	} 
	
	Transition t5 whenMsg resume -> handle_resume
					
	State handle_resume {
		println("WASTESERVICE | resume trolley after sonar resume message.")
		[# TrolleyStatus = "WORKING" #]
		forward led -m led_status : led_status(BLINKS)
		//emit trolley_status : trolley_status(WORKING)
		forward transporttrolley -m resume_trolley : resume_trolley(OK)
	}
	
	Transition t6 whenReply pickup_done -> handle_storage
		 			whenReply storage_done -> storage_Done
					whenReply home_done -> back_done
	
}

QActor transporttrolley context ctxWasteService {
	
	[#
		var Status	= "IDLE";	
		var Position = "HOME";
		var Material = "";
		var PathHome = "";
		var PathIndoor = "";
		var PathContainer = "";
		
		var PathToDo = "";
		var LastState = ""; //to_indoor, to_container, to_home
	#]
	
	State s0 initial {
		printCurrentMessage
		println("the TransportTrolley is waiting..")
	}
	
	Transition t0 whenRequest pickup_request -> move_to_INDOOR
					whenMsg stop_trolley -> home_stopped
					
	State home_stopped {
		println("TRANSPORT TROLLEY | Stopped at home")
	}
	
	Transition t1 whenMsg resume_trolley -> s0
	
	State move_to_INDOOR {
		onMsg(pickup_request : pickup_request(PATH_TO_INDOOR, MATERIAL)){
		 	[# 
		 		PathIndoor = payloadArg(0)
		 		Material = payloadArg(1)
		 	#]
		}
		printCurrentMessage
		println("TRANSPORT TROLLEY | execute the path $PathIndoor to INDOOR.")
		request pathexec -m dopath : dopath($PathIndoor)
		[# Status = "WORKING" #]
		[# Position = "GENERIC" #] 
		//emit trolley_status : trolley_status($Status) 
		//emit trolley_position : trolley_position($Position)
		[# LastState = "to_indoor" #]
	}
	
	Transition t1 whenReply dopathdone -> pickup_action
					whenReply dopathfail -> pathfail
				  	whenMsg stop_trolley -> handle_path
				
	State pickup_action {
		onMsg (dopathdone : dopathdone( ARG )) {
			delay 3000
			run  unibo.kotlin.planner22Util.updateMapWithPath( PathIndoor )
			run  unibo.kotlin.planner22Util.showCurrentRobotState()
			println("TRANSPORT TROLLEY | arrived to INDOOR.")
			[# Position = "INDOOR" #]
			//emit trolley_position : trolley_position($Position)
			replyTo pickup_request with pickup_done : pickup_done(DONE)
			println("TRANSPORT TROLLEY | pick up done.")
		}
	}			
	
	Transition t2 whenRequest storage_request -> move_to_Container
					whenMsg stop_trolley -> indoor_stopped
					
	State indoor_stopped {
		println("TRANSPORT TROLLEY | Stopped at Indoor")
	}
	
	Transition t3 whenMsg resume_trolley -> pickup_action
					  
	State pathfail {
		printCurrentMessage
		println("TRANSPORT TROLLEY| Error: Path fail!") 
	}
	
	State move_to_Container{
		printCurrentMessage
		onMsg(storage_request : storage_request(PATH_TO_CONTAINER)){
		 	[# 
		 		PathContainer = payloadArg(0)
		 	#]
		}
		println("TRANSPORT TROLLEY | execute the path $PathContainer to $Material container ")
		[# Status = "WORKING" #]
		//emit trolley_status : trolley_status($Status)
		request pathexec -m dopath : dopath($PathContainer) 
		[# LastState = "to_container" #]
	}
	
	Transition t4 whenReply dopathdone -> settle_action
				  	whenReply dopathfail -> pathfail
				  	whenMsg stop_trolley -> handle_path
				 
				
	State settle_action {
		onMsg (dopathdone   : dopathdone( ARG )) {
			delay 3000
			run  unibo.kotlin.planner22Util.updateMapWithPath( PathContainer )
			run  unibo.kotlin.planner22Util.showCurrentRobotState()
			println("TRANSPORT TROLLEY | arrived to $Material container")
			if[# Material.equals("plastic")#]{
				[# Position = "CONTAINERP" #]
			} else {
				[# Position = "CONTAINERG" #]
			}
			//emit trolley_position : trolley_position($Position)
			[# Status = "IDLE" #]
			//emit trolley_status : trolley_status($Status)
			run  unibo.kotlin.planner22Util.showCurrentRobotState()
			delay 1500
			println("TRANSPORT TROLLEY | settled $Material on the Container.")
			replyTo storage_request with storage_done : storage_done(DONE)	
		}
		
	}
	
	Transition t5 whenRequest pickup_request -> move_to_INDOOR
	 			  whenRequest home_request -> move_to_HOME
	 			  	whenMsg stop_trolley -> container_stopped
					
	State container_stopped {
		println("TRANSPORT TROLLEY | Stopped at container")
	}
	
	Transition t6 whenMsg resume_trolley -> settle_action
	
	State move_to_HOME{
		printCurrentMessage
		println("TRANSPORT TROLLEY | coming back to HOME ")
		onMsg(home_request : home_request(PATH_TO_HOME)){
			[#
				PathHome = payloadArg(0)
			#]
			request pathexec -m dopath : dopath($PathHome)
			delay 1500
		}
		[# LastState = "to_home" #]	
	}
	
	Transition t5 whenReply dopathdone -> back_to_home
				  	whenReply dopathfail -> pathfail
				  	whenMsg stop_trolley -> handle_path
	
	State back_to_home {
		onMsg (dopathdone   : dopathdone( ARG )) {
			println("TRANSPORT TROLLEY | arrived HOME.")
			[# Position = "HOME" #]
			run  unibo.kotlin.planner22Util.showCurrentRobotState()
			//emit trolley_position : trolley_position($Position)
			replyTo home_request with home_done : home_done(DONE)
		}
	}
	
	Transition t6 whenRequest pickup_request -> move_to_INDOOR
				  
	State handle_path {
		println("TRANSPORT TROLLEY | Trolley stopped by the sonar, stop the pathexec..")
		emit alarm : alarm(STOP)	
		//[# TrolleyStatus = "STOPPED" #]
		//emit trolley_status : trolley_status(STOPPED)
	} 
	
	
	Transition t7 whenReply dopathdone -> trolley_stopped 
					whenReply dopathfail -> savepath 
	
	State savepath {
		printCurrentMessage
		onMsg(dopathfail: dopathfail(PATH)){
			[#
				PathToDo = payloadArg(0);
			#]	
		}
		println("TRANSPORT TROLLEY | SAVE PATH: $PathToDo")
	} 
	
	Goto trolley_stopped
	
	State trolley_stopped {
		printCurrentMessage
		println("TRANSPORT TROLLEY | STOPPED")
		//[# TrolleyStatus = "STOPPED" #]
		//emit led_status : led_status(ON)
		//emit trolley_status : trolley_status(STOPPED)
	}
	
	Transition t7 whenMsg resume_trolley -> handle_resume
					
	State handle_resume {
		println("TRANSPORT TROLLEY | resume trolley after sonar resume message.")
		//emit trolley_status : trolley_status(WORKING)
	}
	
	Goto trolley_resumed
	
	State trolley_resumed {
		printCurrentMessage
		println("TRANSPORT TROLLEY | RESUME")
		//emit trolley_status : trolley_status(WORKING)
		
		println("TRASNSPORT TROLLEY | Last state of trolley: $LastState")
		
		//if [# RemainPath != "" #]{
		request pathexec -m dopath : dopath($PathToDo) 
		//}
		[# PathToDo = "" #]
		
	}
	
	Transition t8 whenReply dopathdone and [# LastState == "to_indoor" #] -> pickup_action
				  whenReply dopathdone and [# LastState == "to_container" #] -> settle_action
				  whenReply dopathdone and [# LastState == "to_home" #] -> back_to_home
				  whenReply dopathfail -> pathfail
	
}
