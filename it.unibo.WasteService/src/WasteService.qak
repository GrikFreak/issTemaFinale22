System wasteservice

Request waste_request : waste_request(MATERIAL, TRUCKLOAD)
Reply loadaccept : loadaccept(MATERIAL, TRUCKLOAD)
Reply loadrejected : loadrejected(MATERIAL, TRUCKLOAD)

Dispatch execute : execute(MATERIAL)

Event containers_weight : containers_weight(GWEIGHT, PWEIGHT)

Event trolley_status : trolley_status(TROLLEY_STATUS)
Event trolley_position : trolley_position(POSITION)

Event change_led : change_led(LED_STATUS)
Event led_status : led_status(LED_STATUS)
 
Dispatch stop : stop(DISTANCE)
Dispatch resume : resume(DISTANCE) 

Context ctxWasteService ip [ host="localhost" port=8025 ]

QActor wastetruck context ctxWasteService {
	
	[#
		var Material 	= "";
		var TruckLoad 	= 0L;
	#]
	
	State s0 initial {
		printCurrentMessage
		println("the WasteTruck is waiting..")
		delay 3000
		
		[# val Load = kotlin.random.Random.nextLong(10,100) #]
		request wasteservice -m waste_request : waste_request(plastic, $Load)
	}
	
	Transition t0 whenReply loadaccept -> accepted
				  whenReply loadrejected -> rejected 
				  
	State accepted {
		printCurrentMessage
		onMsg(loadaccept : loadaccept(MATERIAL, TRUCKLOAD)) {
			[#
				Material 	= payloadArg(0);
				TruckLoad 	= payloadArg(1).toLong();
			#]
			println("WasteTruck | accepted $Material load with weight $TruckLoad")
		}
	}
	
	// if de-commented the system will loop with other waste request 
	// Goto s0
	
	State rejected {
		printCurrentMessage
		onMsg(loadrejected : loadrejected(MATERIAL, TRUCKLOAD)) {
			[#
				Material 	= payloadArg(0);
				TruckLoad 	= payloadArg(1).toLong();
			#]
			println("WasteTruck | rejected $Material load with weight $TruckLoad")
		}
	}
	
	// if de-commented the system will loop with other waste request 
	// Goto s0
}

QActor wasteservice context ctxWasteService {
	
	[# 
		var MAXPB = 500L;
		var MAXGB = 500L;
		var CurrentPB = 0L;
		var CurrentGB = 0L;
	#]
	
	State s0 initial {
		printCurrentMessage
		println("the WasteService is waiting..")
	}
	
	Transition t0 whenRequest waste_request -> handle_request
				  
	State handle_request {
		printCurrentMessage
		onMsg(waste_request : waste_request(MATERIAL, TRUCKLOAD)) {
			[#
				var Material 	= payloadArg(0);
				var TruckLoad 	= payloadArg(1).toLong();
			#]
			println("WasteService | arrived request: $Material load with weight $TruckLoad")
			if [# Material.equals("plastic") #] {
				if [# TruckLoad + CurrentPB <= MAXPB #]{
					replyTo waste_request with loadaccept : loadaccept(plastic, $TruckLoad) 
					[# CurrentPB += TruckLoad #]
					println("WasteService | current plastic weight: $CurrentPB")
					forward transporttrolley -m execute : execute(plastic)
				} else {
					replyTo waste_request with loadrejected : loadrejected(plastic, $TruckLoad)
				}
			} else {
				if [# TruckLoad + CurrentGB <= MAXGB #]{
					replyTo waste_request with loadaccept : loadaccept(glass, $TruckLoad) 
					[# CurrentGB += TruckLoad #]
					println("WasteService | current glass weight: $CurrentGB")
					forward transporttrolley -m execute : execute(glass)
				} else {
					replyTo waste_request with loadrejected : loadrejected(glass, $TruckLoad)
				}
			}
			emit containers_weight : containers_weight($CurrentGB, $CurrentPB)
		}
	}
	
	Transition t1 whenEvent trolley_status -> handle_led
	
	State handle_led {
		printCurrentMessage
		onMsg(trolley_status : trolley_status(TROLLEY_STATUS)){
			[# var Trolley_Status = payloadArg(0) #]
			println("WasteService | Received new trolley_status: $Trolley_Status")
			if [# Trolley_Status.equals("IDLE") #]{
				[# var Led_Status = "LED_OFF" #]
				emit change_led : change_led($Led_Status)
			}
			if [# Trolley_Status.equals("MOVING") #]{
				[# var Led_Status = "LED_BLINKS" #]
				emit change_led : change_led($Led_Status)
			} 
			if [# Trolley_Status.equals("STOPPED") #]{
				[# var Led_Status = "LED_ON" #]
				emit change_led : change_led($Led_Status)
			} 
		}
	}
	
	Goto s0
	
}

QActor transporttrolley context ctxWasteService {
	
	[#
		var Trolley_Status	= "IDLE";	
		var Position = "HOME";
		var Material = "";
	#]
	
	State s0 initial {
		printCurrentMessage
		println("the transportTrolley is waiting..")
	}
	
	Transition t0 whenMsg execute -> move_to_INDOOR
	
	State move_to_INDOOR {
		onMsg(execute : execute(MATERIAL)){
		 	[# Material = payloadArg(0) #]
		}
		printCurrentMessage
		println("transportTrolley | moving to INDOOR")
		[# Trolley_Status = "MOVING" #]
		[# Position = "GENERIC" #]
		emit trolley_status : trolley_status($Trolley_Status)
		emit trolley_position : trolley_position($Position)
		delay 500 
		println("transportTrolley | arrived to INDOOR")
		println("transportTrolley | picking up $Material")
		[# Position = "INDOOR" #]
		emit trolley_position : trolley_position($Position)
		
	}
	
	Goto move_to_ContainerP if [# Material.equals("plastic") #] else move_to_ContainerG
	
	State move_to_ContainerP{
		printCurrentMessage
		println("transportTrolley | moving to plastic container ")
		[# Trolley_Status = "MOVING" #]
		emit trolley_status : trolley_status($Trolley_Status)
		delay 500 
		println("transportTrolley | arrived to plastic container")
		[# Position = "CONTAINERP" #]
		emit trolley_position : trolley_position($Position)
		println("transportTrolley | settling plastic")
		delay 300
		[# Trolley_Status = "IDLE" #]
		emit trolley_status : trolley_status($Trolley_Status)
	}
	
	Goto move_to_HOME
	
	State move_to_ContainerG{
		printCurrentMessage
		println("transportTrolley | moving to glass container ")
		[# Trolley_Status = "MOVING" #]
		emit trolley_status : trolley_status($Trolley_Status)
		delay 500 
		println("transportTrolley | arrived to glass container")
		[# Position = "CONTAINERG" #]
		emit trolley_position : trolley_position($Position)
		println("transportTrolley | settling glass")
		delay 300
		[# Trolley_Status = "IDLE" #]
		emit trolley_status : trolley_status($Trolley_Status)
	}
	
	Goto move_to_HOME
	
	State move_to_HOME{
		printCurrentMessage
		println("transportTrolley | coming back to HOME ")
		delay 500 
		println("transportTrolley | arrived HOME")
		[# Position = "HOME";
			Trolley_Status = "IDLE";	
		#]
		emit trolley_position : trolley_position($Position)
		emit trolley_status : trolley_status($Trolley_Status)
	}
	
	Transition t1 whenMsg stop -> stop_trolley
				  whenMsg resume -> resume_trolley
	
	State stop_trolley {
		printCurrentMessage
		println("TransportTrolley | the transport_trolley has been stopped.")
		[# Trolley_Status = "STOPPED" #]
		emit trolley_status : trolley_status($Trolley_Status)
	}
	
	State resume_trolley {
		printCurrentMessage
		println("TransportTrolley | the transport_trolley has been resumed.")
		[# Trolley_Status = "MOVING" #]
		emit trolley_status : trolley_status($Trolley_Status)
	}
	
}

QActor led context ctxWasteService {
	
	[#
		var Led_Status	= "LED_OFF";
	#]
	
	State s0 initial {
		printCurrentMessage
		println("the Led is off..")
	}
	
	Transition t0 whenEvent change_led -> change_status
				  
	State change_status {
		printCurrentMessage
		onMsg(change_led : change_led(LED_STATUS)){
			[# Led_Status = payloadArg(0) #]
			println("Led | led status changed in: $Led_Status")
			emit led_status : led_status($Led_Status)
		}
	}	
}

QActor sonar context ctxWasteService {
	
	[#
		var DLIMIT = 30L;
		var Stopped = false;
	#]
	
	State s0 initial {
		printCurrentMessage
		println("the sonar is active..")
	}
		
	Transition t0 whenTime 1 -> detect
				  
	State detect {
		printCurrentMessage
		[# val Distance = kotlin.random.Random.nextLong(10,100) #]
		if [# Distance <= DLIMIT && !Stopped #]{
			forward transporttrolley -m stop : stop(Distance)
			[# Stopped = true #]
		}
		if [# Distance > DLIMIT && Stopped #]{
			forward transporttrolley -m resume : resume(Distance)
			[# Stopped = false #]
		}
	}	
}


